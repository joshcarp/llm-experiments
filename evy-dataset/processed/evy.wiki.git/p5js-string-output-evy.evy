x:[]num
y:[]num
clear "black"

for range 20
    x = x + [0]
    y = y + [0]
end

segLength := 3

width 1
color "#ffffffb0"

on move x:num y:num
    draw x y
end

func draw mouseX:num mouseY:num
    clear "black"
    drawSegment 0 mouseX mouseY
    for i := range (len x)-1
        drawSegment i+1 x[i] y[i]
    end
end

func drawSegment i:num xin:num yin:num
    dx := xin - x[i]
    dy := yin - y[i]
    angle := atan2 dy dx
    x[i] = xin - (cos angle) * segLength
    y[i] = yin - (sin angle) * segLength
    move x[i] y[i]
    line x[i]+dx y[i]+dy
end
