
func minIncrementForUnique:num A:[]num
    if A == []
        return 0
    end
    res := 0
    numSet := {}
    duplicate := []num
    sort A true
    left := A[0]
    right := A[-1]
    holes := right - left + 1
    for v := range A
        if has numSet (sprint v)
            duplicate = duplicate + [v]
        else
            numSet[sprint v] = true
        end
    end
    holes = holes - (len numSet)
    for hole := range left+1 right
        if holes == 0 or (len duplicate) == 0
            break
        end
        if !(has numSet (sprint hole)) and hole > duplicate[0]
            res = res + (hole - duplicate[0])
            duplicate = duplicate[1:]
            holes = holes - 1
        end
    end
    while (len duplicate) != 0
        right = right + 1
        res = res + (right - duplicate[0])
        duplicate = duplicate[1:]
    end
    return res
end

func sort arr:[]num inc:bool
    // bubble sort
    for endIdx := range (len arr)-1 0 -1
        for i := range endIdx
            if (inc and arr[i] > arr[i + 1]) or (!inc and arr[i] < arr[i + 1])
                tmp := arr[i]
                arr[i] = arr[i + 1]
                arr[i + 1] = tmp
            end
        end
    end
end

func test
    assert 6 (minIncrementForUnique [3 2 1 2 1 7])
    assert 1 (minIncrementForUnique [0 2 2])
end
test
