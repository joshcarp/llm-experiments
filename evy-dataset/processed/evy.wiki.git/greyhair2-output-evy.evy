// parameters
clear "red"
blackHairs := 550
style := "lin" // "exp" or "lin"
k := 2 // linear
exp := 1.025 // exponent
start := 0.5 // exponent

// internal state
left:[]bool
right:[]bool
leftIdx:[]num
rightIdx:[]num
initData

on animate
    whiten style
    drawHair 40 75 right
    drawFace
    drawHair 30 270 left
    if blackHairs == 0
        exit 0
    end
end

func whiten s:string
    if s == "exp"
        // exponential
        whitenCnt (round start)
        start = start * exp
    else
        whitenCnt k // linear
    end
end

func whitenCnt cnt:num
    for range cnt
        if blackHairs <= 0
            return
        end
        blackHairs = blackHairs - 1
        left[leftIdx[blackHairs]] = false
        right[rightIdx[blackHairs]] = false
    end
end

func drawHair x:num rot:num hair:[]bool
    width 0.1
    fill "none"
    d := 0
    for i := range (len hair)
        if hair[i]
            stroke "black"
        else
            stroke "white"
        end
        ellipse x+d 64-d 31 21 rot 180 0
        d = d + 0.05
    end
end

func drawFace
    color "gold"
    move 50 50
    circle 20
    color "black"
    width 2
    move 47 50
    line 52 48
    move 60 48
    line 62.5 49.5
end

func initData
    for i := range blackHairs
        left = left + [true]
        right = right + [true]
        leftIdx = leftIdx + [i]
        rightIdx = rightIdx + [i]
    end

    shuffle leftIdx
    shuffle rightIdx
end

func shuffle arr:[]num
    for x := range (len arr) 0 -1
        i := rand x
        h := arr[i]
        arr[i] = arr[x - 1]
        arr[x - 1] = h
    end
end
