func findMedianSortedArrays:num nums1:[]num nums2:[]num
    ls1 := len nums1
    ls2 := len nums2
    if ls1 < ls2
        return findMedianSortedArrays nums2 nums1
    end
    l := 0
    r := ls2 * 2
    while l <= r
        mid2 := (l + r) / 2
        mid1 := ls1 + ls2 - mid2
        R1:num
        R2:num
        L1:num
        L2:num
        if mid1 == 0
            L1 = -2147483648
        else
            L1 = nums1[floor ((mid1 - 1) / 2)]
        end
        if mid2 == 0
            L2 = -2147483648
        else
            L2 = nums2[floor ((mid2 - 1) / 2)]
        end
        if mid1 == 2 * ls1
            R1 = 2147483647
        else
            R1 = nums1[floor (mid1 / 2)]
        end
        if mid2 == 2 * ls2
            R2 = 2147483647
        else
            R2 = nums2[floor (mid2 / 2)]
        end
        if L1 > R2
            l = mid2 + 1
        else if L2 > R1
            r = mid2 - 1
        else
            return ((max L1 L2) + (min R1 R2)) / 2
        end
    end
    return -1
end

func test
    print (findMedianSortedArrays [1 1] [1 2])
end
test